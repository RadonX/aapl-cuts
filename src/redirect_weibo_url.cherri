/*
	Extracts and resolves Weibo's shortened t.cn URL to its original destination.

	Input:
	- `ShortcutInput`: A text input containing an http://t.cn URL.

	Output:
	- If a valid URL is found in the t.cn web page, outputs the resolved URL.
	- If no URL is found, outputs the t.cn webpage.
	- Outputs nothing if the input does not contain a valid t.cn URL.
*/
#define inputs text,url
#define noinput askfor text
#define color darkblue
#define glyph compass

@input_text: text
const input_type = typeOf(ShortcutInput)
if input_type == "URL" {
	@input_text = ShortcutInput['Name']
} else {
	@input_text = ShortcutInput
}

const t_cn_links = matchText("(http://t.cn/\\w+)", input_text, false)
// Get the first t.cn URL from the input text.
const t_cn_link = getMatchGroup(t_cn_links, 0)
if !t_cn_link {
	stop()
}

// TODO: Try Article, t_cn_url['Text'] and compare performance
const t_cn_web = getWebpageContents(t_cn_link)
const t_cn_content = getText(t_cn_web) // plain text

const urls_on_t_cn = getURLs(t_cn_content)
if urls_on_t_cn {
	output("{urls_on_t_cn}")
} else {
	output(t_cn_content)
}
