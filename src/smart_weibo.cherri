#define inputs text
#define noinput getclipboard
#define glyph fire
#define color orange
#define version 17
#include 'app_store/Actions.cherri'

@shortcut_input = ShortcutInput
@output_shortcut = "Share To +" // TODO: update shortcut

// Overwrites @shortcut_input and @output_shortcut only for testing.
// This include is skippable in production.
#include 'includes/overwrite_shortcut_io.cherri'

if shortcut_input {
    const text_or_url = run("url_or_text", shortcut_input)
    const input_type = typeOf(text_or_url)
}

if input_type == "URL" {
    if text_or_url beginsWith "https://mp.weixin.qq.com" {
	    run("redirect_to_wechat", text_or_url)
        output("Redirected to WeChat: {text_or_url}")
    }
    @external_url = run("redirect_weibo_url", text_or_url)
    if !external_url {
        if text_or_url !contains "weibo" {
            @external_url = text_or_url
        }
    }
    if external_url {
        const output_for_t_cn_input = run(output_shortcut, external_url)
        output(output_for_t_cn_input)
    }

    @weibo_url = text_or_url
}

if !weibo_url {
    if shortcut_input {
		menu nil {
		item "Share: {shortcut_input}":
			const output_for_text_input = run(output_shortcut, shortcut_input)
			output(output_for_text_input)
		item "Fetch Current Post":
            // Proceed to get the current Weibo post URL below.
		}
	}
	@weibo_url = getOnScreenContent()
    if !weibo_url {
        stop()
    }
    const log_msg = "SmartWeibo | Get on screen post {weibo_url} "
    run("debug__Logging", log_msg) // TODO: update shortcut
}

const weibo_link = "{weibo_url}" // Force text conversion
if weibo_link contains "://weibo.com/" {
    // The OnScreen Weibo URL requires login. Convert it to a mobile URL.
	// const converted_link = replaceText("//weibo\\.com/\\d+", "//m.weibo.cn/status", weibo_link, false, true)
	const converted_link = replaceText("//weibo\\.com/", "//m.weibo.cn/", weibo_link, false, true)
    @weibo_url = url(converted_link)
}

const article = getArticle(weibo_url)
const content_word_count = count(article, "Characters")

if content_word_count == 0 {
    showNotification("No article at {weibo_url}", "", false)
    const web_page_as_rich_text = getWebpageContents(weibo_url)
    // const html_of_rendered_page = makeHTML(web_page_as_rich_text, true)
    // const compact_web_page = getRichTextFromHTML(html_of_rendered_page)
    @url_content = makeMarkdown(web_page_as_rich_text) // getText() or implicit text conversion drops hyperlinks
    @url_content = "{weibo_url}\n\n{url_content}" // Append web content to the URL, because the fetching may fail.
} else {
    @url_content = article['Body']
}

if content_word_count > 500 {
/*
    TODO:
    Many actions doesn't work with long input. 
    www.decarpentier.nl/carpentopod works with show() 
    Weibo status doesnâ€™t 
*/
	show(url_content) // show() text drops hyperlinks
}
run(output_shortcut, url_content) // url_content is a rich text
