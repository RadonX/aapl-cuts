#define inputs text
#define noinput getclipboard
#define glyph fire
#define color orange
#define version 17
#include 'app_store/Actions.cherri'

@shortcut_input = ShortcutInput
@output_shortcut = "Share To +" // TODO: update shortcut

// Overwrites @shortcut_input and @output_shortcut in the include only for testing.
// This include is skippable in production.
#include 'includes/shortcut_input_as_test_config.cherri'

if shortcut_input {
    const text_or_url = run("url_or_text", shortcut_input)
    const input_type = typeOf(text_or_url)
}

if input_type == "URL" {
    if text_or_url beginsWith "https://mp.weixin.qq.com" {
	    run("redirect_to_wechat", text_or_url)
        output("Redirected to WeChat: {text_or_url}")
    }
    @external_url = run("redirect_weibo_url", text_or_url)
    if !external_url {
        if text_or_url !contains "weibo" {
            @external_url = text_or_url
        }
    }
    if external_url {
        const output_for_t_cn_input = run(output_shortcut, external_url)
        output(output_for_t_cn_input)
    }

    @weibo_url = text_or_url
}

if !weibo_url {
    if shortcut_input {
		menu nil {
		item "Share: {shortcut_input}":
			const output_for_text_input = run(output_shortcut, shortcut_input)
			output(output_for_text_input)
		item "Fetch Current Post":
            // Proceed to get the current Weibo post URL below.
		}
	}
	@weibo_url = getOnScreenContent()
    if !weibo_url {
        stop()
    }
    const log_msg = "SmartWeibo | Get on screen post {weibo_url} "
    run("debug__Logging", log_msg) // TODO: update shortcut
}

const raw_url = "{weibo_url}" // Force text conversion
if raw_url contains "://weibo.com/" {
    // The OnScreen Weibo URL requires login. Convert it to a mobile URL.
	@weibo_url = replaceText("//weibo\\.com/\\d+", "//m.weibo.cn/status", raw_url, false, true)
}

const url_content = getArticle(weibo_url)
const content_word_count = count(url_content, "Characters")
if content_word_count > 500 {
/*
    TODO:
    Many actions doesn't work with long input. 
    www.decarpentier.nl/carpentopod works with show() 
    Weibo status doesnâ€™t 
*/
	show(url_content)
}
run(output_shortcut, url_content)
