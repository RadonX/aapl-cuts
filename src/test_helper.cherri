if ShortcutInput {
    @input_as_dict: dictionary
    @input_as_dict = getDictionary(ShortcutInput)
    // We need to use GetValue rather than [] to ensure the dict values are compatible as action params.
    @shortcut_name = getValue(input_as_dict, "shortcut")
    @shortcut_input = getValue(input_as_dict, "input")
    @input_type = "{input_as_dict['input_type']}" // cast input_type to text
    if input_type != "" {
        if input_type == "RichText" {
            @shortcut_input = getRichTextFromHTML(shortcut_input)
        } else {
            if input_type == "URL" {
                @shortcut_input = url(shortcut_input)
            } else {
                showNotification("Input type {input_type} conversion is not supported.")
            }
        }
    }
} else {
    @shortcut_name = prompt("Shortcut to test: ")
    @shortcut_input = prompt("Shortcut input: ")
}

const shortcut_output = run(shortcut_name, shortcut_input)
const output_type = typeOf(shortcut_output)
const output_count = count(shortcut_output)
showNotification("{shortcut_name} outputs {output_count} item(s): {output_type}")

const serialized_output = run("test_helper-serialize_output", shortcut_output)
output(serialized_output)
