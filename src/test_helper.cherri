if ShortcutInput {
    const input_as_dict = getDictionary(ShortcutInput)
    // We need to use GetValue rather than [] to ensure the dict values are cast to Text.
    @shortcut_name = getValue(input_as_dict, "shortcut")
    @shortcut_input = getValue(input_as_dict, "input")
} else {
    @shortcut_name = prompt("Shortcut to test: ")
    @shortcut_input = prompt("Shortcut input: ")
}

const shortcut_output = run(shortcut_name, shortcut_input)
const output_type = typeOf(shortcut_output)
const output_count = count(shortcut_output)
showNotification("{shortcut_name} outputs {output_count} item(s): {output_type}")

if output_count == 0 {
    stop()
}
if output_count == 1 {
    if output_type == "URL" {
        output("<URL><Name>{shortcut_output['Name']}</Name></URL>")
    } else {
        output("{shortcut_output}")
    }
}

@output_text: text
for output_item in shortcut_output {
    const item_type = typeOf(output_item)
    if item_type == "URL" {
        @output_text += "<URL><Name>{output_item['Name']}</Name></URL>\n\n"
    } else {
        @output_text += "{output_item}\n\n"
    }
}
output(output_text)
