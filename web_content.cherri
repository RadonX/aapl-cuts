/*
 * Handles various input types and processes them into corresponding web content:
 *
 * Input:
 * - `ShortcutInput`: Any web-related types such as "Article", "Safari web page", or "URL".
 * - If input is not provided, it will prompt the user for input.
 *
 * Output:
 * - If the input type is compatible with text extraction (e.g., Article, Safari web page):
 *   Outputs all extracted text, including the title.
 * - If the input type is compatible with articles (e.g., URL):
 *   Outputs the body of the article.
 * - Otherwise:
 *   Outputs .webarchive file.
 */
#define color blue
#define inputs article,richtext,webpage,text,url
// My iOS version is still 17.5. The conditional statement is different for iOS 18.
#define version 17

const input_type = typeOf(ShortcutInput)
showNotification(input_type.text, "", false, ShortcutInput)

const text_compatible_type = "Article, Safari web page" // a.k.a Safari Reader
if text_compatible_type contains input_type {
	// The `getText` action is compatible with many types of input.
	// It’s faster. It extracts all the text, including the title.
	// Alternative: `getArticleDetail(ShortcutInput, "Body")` which excludes the title, etc.
	const text = getText(ShortcutInput)
	mustOutput(text, text)
}

// Note that in English system, a Safari web page will be captured by the `getText` action above.
// I am still testing this shortcut to decide the best way to handle Safari web pages.
const article_compatible_type = "Safari web page, URL, Safari浏览器网页"
if article_compatible_type contains input_type {
	// This action only extracts main content, and includes formatted text.
	const article = getArticle(ShortcutInput) // a.k.a Safari Reader
	mustOutput(article['Body'], article['Body'])
}

// TODO: a smarter criteria to decide if a URL is text_compatible_type or article_compatible_type. 
// iCloud or YouTube links should reach here. They are not compatible with Article.

if !ShortcutInput {
	@other_type_input = "{Ask}"
} else {
	@other_type_input = ShortcutInput
}
const contents_of_web_page = getWebpageContents(other_type_input)
output(contents_of_web_page)

// Below is unreachable code.
// Fetch HTML file (Rich text), equivalent to `getWebpageContents`, but without redirect.
downloadURL(ShortcutInput)
